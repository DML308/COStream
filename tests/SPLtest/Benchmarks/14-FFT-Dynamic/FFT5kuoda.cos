#define PI 3.141592654

//double Wn[N/2] = {0};

composite Combine(output stream<double x>Out, input stream<double x>In)
{
	param 
		int TN;

	Out = CombineDFTX(In)
	{
		double wn_r = cos(2 * PI / TN);
		double wn_i = sin(2 * PI / TN);
		work
		{
			int i;
			double w_r = 1, w_i = 0;
			double results[2 * TN];
			double y0_r, y0_i, y1_r, y1_i;
			double y1w_r, y1w_i, w_r_next, w_i_next;

			for (i = 0; i < TN; i += 2)
			{
				y0_r = In[i].x;
				y0_i = In[i+1].x;

				y1_r = In[TN+i].x;
				y1_i = In[TN+i+1].x;

				y1w_r = y1_r * w_r - y1_i * w_i;
				y1w_i = y1_r * w_i + y1_i * w_r;

				results[i] = y0_r + y1w_r;
				results[i + 1] = y0_i + y1w_i;

				results[TN + i] = y0_r - y1w_r;
				results[TN + i + 1] = y0_i - y1w_i;

				w_r_next = w_r * wn_r - w_i * wn_i;
				w_i_next = w_r * wn_i + w_i * wn_r;
				w_r = w_r_next;
				w_i = w_i_next;
			}

			for (i = 0; i < 2 * TN; i++)
			{
				Out[i].x = results[i];
			}
		}
		window{
			In  sliding(2 * TN,2 * TN);
			Out tumbling(2 * TN);
		} 
	};
}

composite SecondSource(output stream<double x>Out, input stream<double x>In)
{
	param
	    int N;
	Out = AssignmentX(In)
	{
		work
		{
			int i;
			for (i = 0; i<N*2; i++) 
			{
			    Out[i].x = In[0].x + i;
			}
		}
		window {
	    In  sliding(1,1);
		Out	tumbling(N*2);
	    }
	};
}

composite FFTReorder(output stream<double x>Out, input stream<double x>In)
{
	param 
		int n;
	Out = FFTReorderSimpleX(In)
	{
		work
		{
		    int i,j,k;
			int m = 0;
			int temp[2*n];
			int final[2*n];
			for(i = 0; i < 2*n; i++)
			    final[i] = i;
			for(i = 2; i < n; i *= 2)
			{
			    for(j = 0; j < i/2; j++)
				{
				    for(k = 0 + 4*n*j/i; k < n*4*(j+1)/i; k += 4)
					{
					   temp[m++] = final[k];
					   temp[m++] = final[k+1];
					}
					for(k = 2 + 4*n*j/i; k < n*4*(j+1)/i; k += 4)
					{
					   temp[m++] = final[k];
					   temp[m++] = final[k+1];
					}
				}
				for(j = 0; j < 2*n; j++)
				    final[j] = temp[j];
				m = 0;
			}
			for(i = 0; i < 2*n; i++)
			    Out[final[i]].x = In[i].x;
		}
		window {
		    In sliding(2*n,2*n);
			Out tumbling(2*n);
		}
	};
}

composite CombineDFT(output stream<double x>Out, input stream<double x>In)
{
	param 
		int n;
	Out = pipeline(In)
	{
		int j;
		for(j=2; j<=n; j*=2)
			add Combine(j);
	};
}

composite uncer(output stream<double x>Out, input stream<double x>In)
{
	Out = uncers(In)
	{
		work{
			int t = 0;
			int i = 0;
			for(i = 0;i<512;i++)
			{
				Out[t].x = In[i].x+5;
				t++;
			}
		}
		window
		{
			In tumbling(512);
			Out uncertainty();
		}
	};
}
composite Main()
{
    //int P[2*N];
	int N = 512;
	stream<double x> Source,CDFT;
	
	Source = FloatSource()
	{
		double max = 1000.0;	
		double current = 0.0;
		work
		{	
			int i;
			if (current > max)
				current = 0.0;
			Source[0].x = current;
			current += 2 * N;
		}
		window {
			Source tumbling(1);
		}
	};
	CDFT = pipeline(Source){
	    add SecondSource(N);
		add FFTReorder(N);
		add CombineDFT(N);
		add uncer();
		add CombineDFT(N);
	};
	FloatSink(CDFT)
	{
		work
		{
			println(CDFT[0].x);
		}
	};
}

